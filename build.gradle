plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'com.diffplug.spotless' version '6.21.0'
}

group = 'com.onetuks'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // TestContainers
    testImplementation 'org.testcontainers:testcontainers:1.19.0'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation "com.redis.testcontainers:testcontainers-redis-junit:1.6.4"

    // AWS S3
    implementation(platform("software.amazon.awssdk:bom:2.20.136"))
    implementation("commons-io:commons-io:2.13.0")
    implementation("software.amazon.awssdk:aws-core")
    implementation("software.amazon.awssdk:sdk-core")
    implementation("software.amazon.awssdk:sts")
    implementation("software.amazon.awssdk:s3")
    testImplementation("org.testcontainers:localstack:1.19.0")

    // Kafka
    implementation("org.springframework.kafka:spring-kafka")
    testImplementation("org.testcontainers:kafka:1.19.0")

    // Arch-Unit
    testImplementation 'com.tngtech.archunit:archunit:1.1.0'

    // Mockito
//    testImplementation 'org.mockito:mockito-core:3.+'
}

jacoco {
    toolVersion = "0.8.8"
}

tasks.named('test') {
    useJUnitPlatform()

    jacoco {}
    finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
        csv.required = false

        xml.destination(file("build/jacoco/jacoco.xml"))
        html.destination(file("build/jacoco/jacoco.html"))
    }

    finalizedBy(tasks.jacocoTestCoverageVerification)
}

tasks.jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            // 클래스 당 커버리지 50% 이상
            limit {
                counter = "LINE"
                value = "COVEREDRATIO"
                minimum = BigDecimal.valueOf(0.5)
            }

            // 클래스 당 100줄 미만
            limit {
                counter = "LINE"
                value = "TOTALCOUNT"
                maximum = BigDecimal.valueOf(100)
            }

            excludes = List.of(
                    "*.controller.*",
                    "com.onetuks.dayonetest.DayonetestApplication",
                    "com.onetuks.dayonetest.MyCalculatorApplication",
            )
        }
    }
}

sonar {
    properties {
        property("sonar.host.url", "http://localhost:9000")
        property("sonar.login", "sqp_4074232e4c9d2f63d5f4f9274c32c5a9420a2694")
        property("sonar.sources", "src/main/java")
        property("sonar.tests", "src/test/java")
        property("sonar.sourceEncoding", "UTF-8")
        property("sonar.projectKey", "dayonetest")
        property("sonar.projectName", "dayonetest")
        property("sonar.converage.jacoco.xmlReportPaths", "build/jacoco/jacoco.xml")
    }
}

spotless {
    java {
        googleJavaFormat()

        removeUnusedImports()
        trimTrailingWhitespace() // 불필요한 공백 트림
        indentWithSpaces(4) // 들여쓰기 4칸
        endWithNewline() // 파일 끝에 개행 추가
    }
}
